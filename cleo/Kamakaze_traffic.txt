// This file was decompiled using SASCM.ini published by GTAG (http://gtagmodding.com/opcode-database/) on 6.14.2013
{$CLEO .cs}
0000:
//-------------MAIN---------------
//22@ = 0xB74494  -- This is the address to the pointers of the info pool of loaded cars

thread 'KAMAKAZE' 
5@ = 0
0@ = False
//0A8C: write_memory 0x96914F size 1 value 1 virtual_protect 0   //Address for aggressive drivers

//0A8C: write_memory 0x969175 size 1 value 1 virtual_protect 0 //peds riot

0A8D: 25@ = read_memory 0xB74490 size 4 virtual_protect 0 //read ped pool size pointer
25@ += 12

0A8D: 18@ = read_memory 0xB74494 size 4 virtual_protect 0   //address for veh pool 
18@ += 12                                                   // pointer for current pool size



:K1
wait 100

if and  //If in game, start script
    Player.Defined($PLAYER_CHAR)
    $ONMISSION == 0
    $ACTIVE_INTERIOR == 0
then
    01EB: set_traffic_density_multiplier_to 100.0
    03DE: set_pedestrians_density_multiplier_to 0.5 
    06D0: enable_emergency_traffic 0 
    099E: enable_police_patrols 1 
    jump @DEL_CAR
end
jump @K1


:MY_CAR  //Set properties of players car when they enter
27@ = Actor.CurrentCar($PLAYER_ACTOR)
020A: set_car 27@ door_status_to 2
02AC: set_car 27@ immunities 1 1 1 1 0
01EC: make_car 27@ very_heavy 1
jump @DEL_CAR




:DEL_CAR //Main loop
0001: wait 10 ms

077E: get_active_interior_to $ACTIVE_INTERIOR
if or //Disable mod when indoors or on mission
    $ONMISSION == 1
    $ACTIVE_INTERIOR <> 0
then
    jump @K0
end



0A8C: write_memory 25@ size 4 value 0 virtual_protect 0 //zero ped pool if high (seems to help?)
0A8C: write_memory 18@ size 4 value 0 virtual_protect 0 //zero veh pool if high (seems to help?)


00A0: store_actor $PLAYER_ACTOR position_to 1@ 2@ 3@

if and
    Actor.Driving($PLAYER_ACTOR) //Get and check bool if player driving
    0@ == False
then
    0@ = True //If player is driving, then make their car invulnerable and locked
    jump @MY_CAR
end

if
    not Actor.Driving($PLAYER_ACTOR) //Used to unlock the doors when player leaves their car
then
    0@ = False 
end


if  //Flip car if player is driving and speed less than 10
    0@ == True
then
    02E3: 28@ = car 27@ speed
    if 
        28@ <= 10.0
    then   
        if 
            01F4:   car 27@ flipped
        then
            0731: set_car 27@ y_angle_to 0 
        end
    end
end



//Clean up dead stuff
if
    0AE2: 4@ = find_all_random_vehicles_in_sphere 1@ 2@ 3@ radius 1500.0 find_next 1 skip_dead 0
then
    00AE: set_car 4@ traffic_behaviour_to 3
    00AD: set_car 4@ max_speed_to 200.0
    09B3: get_car 4@ door_status 31@
        
    //020C: create_explosion_with_radius 13 at 6@ 7@ 8@
    if and
        0119: car 4@ wrecked
        31@ <> 2
    then
        00A6: destroy_car 4@
    end       
end


//Peds jack cars if not in them, flee if in them
0AE1: 9@ = random_actor_near_point 1@ 2@ 3@ in_radius 1000.0 find_next 1 pass_deads 0
if
    0118:   actor 9@ dead
    //not Actor.Driving(9@)
then
    Actor.DestroyInstantly(9@)
else 
    //074F:   actor 9@ ped_event = 31
    if
        not Actor.Model(9@) == 0
    then
        if
            not Actor.Driving(9@)
        then
            00A0: store_actor 9@ position_to 19@ 20@ 21@
            0AE2: 22@ = find_all_random_vehicles_in_sphere 19@ 20@ 21@ radius 10.0 find_next 1 skip_dead 1
            05D2: AS_actor 9@ run_to_and_hijack_car 24@ max_search_radius 10.0 traffic_behavior 2
        else
            0751: AS_actor 9@ flee_from_actor 9@ run_distance 100000.0 time 999999 change_course 0 unknown 3000 5000 away_radius 2000.0
        end
        
        if 
            0491: actor 9@ has_weapon 22 //Remove Colt45 from cop, just to free memory
        then 
            0555: remove_weapon 22 from_actor 9@
        end
        
        if
            0491: actor 9@ has_weapon 3     //Remove night stick from cops, same reason
        then 
            0555: remove_weapon 3 from_actor 9@
        end        
    end        
end

jump @DEL_CAR



:K0   //Disables mod when indoors or on mission
wait 100
077E: get_active_interior_to $ACTIVE_INTERIOR
01EB: set_traffic_density_multiplier_to 1.0

if and
    $ONMISSION == 0
    $ACTIVE_INTERIOR == 0
then
    jump @K1
end
jump @K0




